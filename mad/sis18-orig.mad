!                                    S I S 18
TITLE, " -- SIS -- :  bend. quad. sex.  with correction to fringe "; 
beam, particle=U, energy=0.0114, charge=28;//, mass=238;  
//beam, particle=U, mass=238, charge=28, gamma=1.0132;  
Q_hor = 4.17;
Q_vec = 3.20;


!---------------------  DRIFT SPACES DEFINITION  ---------------------
DR1: DRIFT, L=0.6450000;
DR2: DRIFT, L=0.9700000;
DR3: DRIFT, L=6.8390117;
DR4: DRIFT, L=0.6000000;
DR5: DRIFT, L=0.7098000;
DR6: DRIFT, L=0.4998000;
DRB: DRIFT, L= 2.617993877991;
!---------------------------------------------------------------------

!--------------------------  BENDING MAGNET ---------------------------
ALPHA  := 15 * RADDEG;
LL  := 150 * RADDEG;
PFR := 7.3* RADDEG;
FI  := 0.7;
!fi=0.0;
RB: SBEND,L= LL,ANGLE= ALPHA,E1= PFR,E2=PFR,HGAP= 0.035,FINT= FI;
! ------------------- horizontal quadrupole ----------------------------
QS1F:  QUADRUPOLE, L= 1.0400000,  K1=  0.357893011;
QS2D:  QUADRUPOLE, L= 1.0400000,  K1= -0.335373024;
QS3T:  QUADRUPOLE, L= 0.4804000,  K1=  0.0606702417;



! ---------------------------  LINE DEFINITION ---------------------------
CELLA:  LINE=(DR1,RB,DR2,RB,DR3,QS1F,DR4,QS2D,DR5,QS3T,DR6);
!CELLA:  LINE=(DR1,DRB,DR2,DRB,DR3,QS1F,DR4,QS2D,DR5,QS3T,DR6);
!------------------------------------------------------------------------
MACHINE: LINE=(12*CELLA);

Beam;//, energy=0.4;
USE, sequence=MACHINE;
match, sequence=MACHINE;
	global, sequence=MACHINE, Q1=Q_hor, Q2=Q_vec;    
	vary, name=QS1F->K1, step=2.0e-4;
	vary, name=QS2D->K1, step=2.0e-4;  
	LMDIF, calls=200, tolerance=1.e-21;
endmatch;   


select, flag=sectormap, clear;
select, flag=sectormap, range=#s/#e;
select, flag=twiss, clear;
select, flag=twiss, column=name, keyword, s, L, dmux, dmuy;     
select, flag=error,pattern="^q.*";
twiss, chrom, file=twiss-orig.txt;
twiss, sectormap, sectorfile=sectormap-orig.txt;
stop;
